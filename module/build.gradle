buildscript {
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
        classpath "com.github.townsfolk:gradle-release:1.2"
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "maven-publish"

repositories {
    jcenter()
}

dependencies {
    // Currently, in the POM, the compile dependency will be translated to a runtime dependency due to a known
    // limitation of maven-publish.
    // http://forums.gradle.org/gradle/topics/maven_publish_plugin_generated_pom_making_dependency_scope_runtime
    compile "io.dropwizard:dropwizard-auth:${commonVersions.dropwizard}"
    testCompile "io.dropwizard:dropwizard-testing:${commonVersions.dropwizard}"
    testCompile "org.codehaus.groovy:groovy:2.3.3"
}

sourceCompatibility = "1.7"

group = "com.commercehub.dropwizard"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                artifactId "dropwizard-auth-active-directory"
                classifier "sources"
            }
            artifact javadocJar {
                artifactId "dropwizard-auth-active-directory"
                classifier "javadoc"
            }
        }
    }
}


apply plugin: "bintray"
apply plugin: "release"

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = "main"
        userOrg = "commercehub-oss"
        name = "dropwizard-auth-active-directory"
        desc = "A Very Simple Direct Bind Active Directory Basic Auth Provider for DropWizard"
        licenses = ['Apache-2.0']
        labels = ["rest", "security", "active-directory", "dropwizard"]
    }
}
bintrayUpload.dependsOn build, sourceJar, javadocJar

createReleaseTag.dependsOn bintrayUpload
